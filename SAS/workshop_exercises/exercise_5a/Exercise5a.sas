/**********************************************************************************

DESCRIPTION:  THIS PROGRAM ILLUSTRATES HOW TO CONSTRUCT FAMILY-LEVEL VARIABLES FROM PERSON-LEVEL DATA

              THERE ARE TWO DEFINITIONS OF FAMILY UNIT IN MEPS.
                 1) CPS FAMILY:  ID IS DUID + CPSFAMID.  CORRESPONDING WEIGHT IS FAMWT15C.
                 2) MEPS FAMILY: ID IS DUID + FAMIDYR.   CORRESPONDING WEIGHT IS FAMWT15F.

              THE CPS FAMILY IS USED IN THIS EXERCISE.

INPUT FILE:   C:\MEPS\SAS\DATA\H181.SAS7BDAT (2015 FY PUF DATA)

*********************************************************************************/;
OPTIONS LS=132 PS=79 NODATE FORMCHAR="|----|+|---+=|-/\<>*" PAGENO=1;
FILENAME MYLOG "U:\MEPS\SAS\Exercise_3\Exercise3_log.TXT";
FILENAME MYPRINT "U:\MEPS\SAS\Exercise_3\Exercise3_OUTPUT.TXT";
PROC PRINTTO LOG=MYLOG PRINT=MYPRINT NEW;
RUN;
LIBNAME CDATA 'C:\MEPS\SAS\DATA';
*LIBNAME CDATA "\\programs.ahrq.local\programs\meps\AHRQ4_CY2\B_CFACT\BJ001DVK\Workshop_2017\SAS\Data";

TITLE1 '2018 AHRQ MEPS DATA USERS WORKSHOP';
TITLE2 "EXERCISE3.SAS: CALCULATE FAMILY-LEVEL ESTIMATES";

TITLE3 "SAMPLE DUMP FOR FAMILY IDS";
PROC PRINT DATA=CDATA.H181 (OBS=20);
  VAR PID AGE15X SEX CPSFAMID FAMWT15C FAMIDYR FAMWT15F;
  BY DUID;
RUN;

PROC SORT DATA=CDATA.H181 (KEEP=DUPERSID DUID CPSFAMID FAMWT15C VARSTR VARPSU TOTSLF15 TTLP15X)
              OUT=PERS;
  BY DUID CPSFAMID;
RUN;

DATA PERS2
     FAM (KEEP=DUID CPSFAMID FAMSIZE FAMOOP FAMINC);
 SET PERS;
  BY DUID CPSFAMID;

     LABEL FAMSIZE = '# OF PERSONS PER CPS FAMILY'
           FAMOOP  = 'TOTAL OUT-OF-POCKET EXP (TOTSLF15) PER CPS FAMILY'
           FAMINC  = 'TOTAL INCOME (TTLP15X) PER CPS FAMILY';

     IF FIRST.CPSFAMID THEN DO;
        FAMSIZE = 0 ;
        FAMOOP  = 0 ;
        FAMINC  = 0 ;
     END;

     FAMSIZE + 1        ;
     FAMOOP  + TOTSLF15 ;
     FAMINC  + TTLP15X  ;

     OUTPUT PERS2;
     IF LAST.CPSFAMID THEN OUTPUT FAM;
RUN;
TITLE3 "A SAMPLE DUMP TO CHECK THE CREATION OF THE FAMILY-LEVEL VARIABLES";
PROC PRINT DATA=PERS2 (OBS=20);
  BY DUID CPSFAMID;
RUN;

/*ADD WEIGHT, VARSTR, AND VARPSU TO THE FAMILY-LEVEL ANALYTIC DATA*/

PROC SORT DATA=PERS (WHERE=(FAMWT15C>0)) OUT=FAMWT (KEEP=DUID CPSFAMID FAMWT15C VARSTR VARPSU) NODUPKEY;
  BY DUID CPSFAMID;
RUN;

DATA FAM2;
  MERGE FAM (IN=AA) FAMWT (IN=BB);
  BY DUID CPSFAMID;
  IF AA AND BB;
RUN;

TITLE3 "CPS FAMILY-LEVEL ESTIMATES ON FAMILY SIZE, OUT-OF-POCKET EXP, AND INCOME, 2015";
ods graphics off;
PROC SURVEYMEANS DATA=FAM2 NOBS SUMWGT MEAN STDERR;
	STRATA  VARSTR ;
	CLUSTER VARPSU ;
	WEIGHT  FAMWT15C ;
	VAR FAMSIZE FAMOOP FAMINC;
RUN;
PROC PRINTTO;
RUN;
